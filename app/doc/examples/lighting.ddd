// lighting.ddd

clear_color 0, 0, 0, 0

// Set current color
color "white"

create_light Id, X, Y, Z, Dr, Dg, Db ->
    light Id
    light_ambient 0, 0, 0, 1
    light_diffuse Dr, Dg, Db, 1
    light_position X, Y, Z

red_light X, Y, Z -> create_light 0, X, Y, Z, 1, 0, 0
green_light X, Y, Z -> create_light 1, X, Y, Z, 0, 1, 0
blue_light X, Y, Z -> create_light 2, X, Y, Z, 0, 0, 1

white_sphere R ->
    // Note: material_ambient is visible due to the default
    // OpenGL global ambient light (0.2, 0.2, 0.2, 1.0)
    // These materials will be multiply by current color (white)
    material_ambient 0.3, 0.3, 0.3, 1
    material_diffuse 1.0, 1.0, 1.0, 1.0
    slices -> 50
    sphere 0, 0, 0, R, R, R, slices, slices

draw_scene ->
    d -> window_height
    t -> time
    red_light d * sin t, d * cos t, d
    green_light d * sin ( -t + 120) , d * cos ( -t + 120) , d
    blue_light 0, d * sin t, d * cos t
    white_sphere d * 60%

draw_scene
