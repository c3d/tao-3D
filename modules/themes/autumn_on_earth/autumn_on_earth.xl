// *****************************************************************************
// autumn_on_earth.xl                                              Tao3D project
// *****************************************************************************
//
// File description:
//
//     The 'Autumn on Earth' theme.
//
//     This computer program serves as a document template for
//     the Tao3D application.
//
//
//
//
//
//
// *****************************************************************************
// This software is licensed under the GNU General Public License v3
// (C) 2013-2014, Baptiste Soulisse <baptiste.soulisse@taodyne.com>
// (C) 2012-2014,2019, Christophe de Dinechin <christophe@dinechin.org>
// (C) 2013-2014, Jérôme Forissier <jerome@taodyne.com>
// *****************************************************************************
// This file is part of Tao3D
//
// Tao3D is free software: you can r redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
//
// Tao3D is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
//
// You should have received a copy of the GNU General Public License
// along with Tao3D, in a file named COPYING.
// If not, see <https://www.gnu.org/licenses/>.
// *****************************************************************************

import Slides
import Charts

module_description
    id "EC3C6ED6-1727-4248-9449-61AC6A2D477E"
    name "Autumn on Earth theme"
    description "A simple brown theme"
    import_name "AutumnOnEarthTheme"
    author "Taodyne SAS"
    website "http://www.taodyne.com"
    version 1.004


module_description "fr",
    name "Automne sur terre"
    description "Un simple module à dominantes brunes"

// Adding this module's images/ directory to the AutumnOnEarth: search path
// for the current document
add_search_path "AutumnOnEarth:", module_dir



// ============================================================================
//
//    Theme definition
//
// ============================================================================

theme_slide_width  "AutumnOnEarth" -> 1024
theme_slide_height "AutumnOnEarth" -> 768

theme_drawing_width "AutumnOnEarth" -> 500
theme_drawing_height "AutumnOnEarth" -> 540


// ============================================================================
//
//    Theme charts
//
// ============================================================================

theme_chart_layout "AutumnOnEarth", "pie", Style:text, Format:text ->
// ----------------------------------------------------------------------------
//    Pie chart layout for Autumn theme
// ----------------------------------------------------------------------------
    chart_series 0, 0, 400, 400, "pie", Style, Format

    chart_flow 0, 260, 850, 50, "chart_title"
    chart_flow 425, 0, 300, 400, "chart_legend"


theme_chart_layout "AutumnOnEarth", Master:text, Style:text, Format:text ->
// ----------------------------------------------------------------------------
//    Default chart layout for Autumn theme
// ----------------------------------------------------------------------------
    chart_flow 0, 260, 850, 50, "chart_title"
    chart_flow 425, 0, 300, 400, "chart_legend"

    chart_flow 265, -200, 100, 50, "chart_xlabel"
    chart_flow -475, 225, 100, 50, "chart_ylabel"

    // Draw axis
    chart_xaxis -475, -200, 700, 400, Style, Format
    chart_yaxis -475, -200, 700, 400, Style, Format

    chart_series -475, -200, 700, 400, Master, Style, Format

// Size
theme_size "AutumnOnEarth", Master:text, "chart_data" -> 20
theme_size "AutumnOnEarth", Master:text, "chart_label" -> 20
theme_size "AutumnOnEarth", Master:text, "chart_tick" -> 20
theme_size "AutumnOnEarth", Master:text, "chart_title" -> 30
theme_size "AutumnOnEarth", Master:text, "chart_legend" -> 20

// Alignment of charts components
theme_align "AutumnOnEarth", Master:text, "chart_data" -> align 0.5; vertical_align 0.5
theme_align "AutumnOnEarth", Master:text, "chart_tick" -> align 0.5; vertical_align 0.5
theme_align "AutumnOnEarth", Master:text, "chart_label" -> align 0.5; vertical_align 0.45
theme_align "AutumnOnEarth", Master:text, "chart_title" -> align 0.5; vertical_align 0.5
theme_align "AutumnOnEarth", Master:text, "chart_entry", N:integer -> theme_align Theme,Master,"chart_legend",N
theme_align Theme:text, "pie", "chart_legend" ->
    align 0.0
    vertical_align 0.5
    paragraph_space 0, (30.0 / (chart_count chart_first))
    margins 90, 0
theme_align "AutumnOnEarth", Master:text, "chart_legend" ->
    align 0.0
    vertical_align 0.5
    paragraph_space 0, 30
    margins 90, 0

// Colors
theme_color "AutumnOnEarth", Master:text, "line", N:integer ->
    T := 0.55 / chart_datasets_count
    line_color_hsv 9, 0.65 - T * N, 0.4 + T * N
theme_color "AutumnOnEarth", "pie", Style:text, N:integer ->
    T := 0.55 / (chart_count chart_first)
    color_hsv 9, 0.65 - T * N, 0.4 + T * N

theme_color "AutumnOnEarth", "area", Style:text, N:integer -> theme_color "AutumnOnEarth", "bar", Style, N
theme_color "AutumnOnEarth", "line", Style:text, N:integer -> theme_color "AutumnOnEarth", "bar", Style, N
theme_color "AutumnOnEarth", "bar", Style:text, N:integer ->
    T := 0.55 / chart_datasets_count
    color_hsv 9, 0.65 - T * N, 0.4 + T * N

// ============================================================================
//
//    Colors
//
// ============================================================================

theme_color      "AutumnOnEarth","main","title"           -> color "white"
theme_color      "AutumnOnEarth","main","subtitle"        -> color "black"
theme_color      "AutumnOnEarth","section","title"        -> color "white"
theme_color      "AutumnOnEarth","section","subtitle"     -> color "black"
theme_color      "AutumnOnEarth",Master:text,"title"      -> color "white"
theme_color      "AutumnOnEarth",Master:text,"subtitle"   -> color "#888"
theme_color      "AutumnOnEarth",Master:text,"story"      -> color "black"
theme_font       "AutumnOnEarth",Master:text,"story"      -> font "Gill Sans", "Ubuntu", "Arial"
theme_boxstyle   "AutumnOnEarth",Master:text,"title"      -> color "transparent"
theme_boxstyle   "AutumnOnEarth",Master:text,Style:text   -> color "transparent"

theme_align "AutumnOnEarth","main","title"                -> align 0.5; vertical_align 0.5
theme_align "AutumnOnEarth","main","subtitle"             -> align 0.5; vertical_align 0.5
theme_align "AutumnOnEarth","section","title"             -> align 0.5; vertical_align 0.5
theme_align "AutumnOnEarth","section","subtitle"          -> align 0.5; vertical_align 0.5
theme_align "AutumnOnEarth",Master:text,"title"           -> align 0.0; vertical_align 1.0
theme_align "AutumnOnEarth",Master:text,"subtitle"        -> align 1.0; vertical_align 1.0
theme_align "AutumnOnEarth",Master:text,Style:text        -> align 0.0; vertical_align 0.5


theme_size "AutumnOnEarth",Master:text,"story"             -> 36
theme_size "AutumnOnEarth","main","title"                  -> 56
theme_size "AutumnOnEarth","main","subtitle"               -> 48
theme_size "AutumnOnEarth","section","title"               -> 56
theme_size "AutumnOnEarth","section","subtitle"            -> 48
theme_size "AutumnOnEarth",Master:text,"title"             -> 48
theme_size "AutumnOnEarth",Master:text,"subtitle"          -> 42
theme_size "AutumnOnEarth",Master:text,"story",1           -> 36
theme_size "AutumnOnEarth",Master:text,"story",2           -> 32
theme_size "AutumnOnEarth",Master:text,"story",3           -> 30
theme_size "AutumnOnEarth",Master:text,"story",4           -> 28


// ============================================================================
//
//    Background pictures
//
// ============================================================================

theme_background "AutumnOnEarth", "main" ->
// ----------------------------------------------------------------------------
//   Background for main title slides
// ----------------------------------------------------------------------------
    clear_color 0.70,0.45,0.40,1
    contents -900,
        color "white"
        image 0, 0, 1.1, 1.1, "AutumnOnEarth:brown-main.png"


theme_background "AutumnOnEarth", "section" ->
// ----------------------------------------------------------------------------
//   Background for section slides
// ----------------------------------------------------------------------------
    clear_color 0.70,0.45,0.40,1
    contents -900,
        color "white"
        image 0, 0, 1.1, 1.1, "AutumnOnEarth:brown-section.png"


theme_background "AutumnOnEarth", Master:text ->
// ----------------------------------------------------------------------------
//   Background for other slides (story, etc)
// ----------------------------------------------------------------------------
    clear_color 0.70,0.45,0.40,1
    contents -900,
        color "white"
        image 0, 0, 1.1, 1.1, "AutumnOnEarth:brown-story.png"



// ============================================================================
//
//    Slide layout
//
// ============================================================================

theme_layout "AutumnOnEarth", "main" ->
// ----------------------------------------------------------------------------
//    Flows for main title
// ----------------------------------------------------------------------------
    flow 150, 30, 700, 200, "title"
    locally
        rotatez 8
        flow 0, -160, 700, 150, "subtitle"


theme_layout "AutumnOnEarth", "section" ->
// ----------------------------------------------------------------------------
//    Flows for section title
// ----------------------------------------------------------------------------
    locally
        rotatez -4
        flow 200, 180, 550, 200, "title"
    locally
        rotatez 4
        flow -260, -180, 500, 200, "subtitle"


theme_layout "AutumnOnEarth", Master:text ->
// ----------------------------------------------------------------------------
//    Centered flows
// ----------------------------------------------------------------------------
    flow -220, 280, 500, 120, "title"
    flow 270, 260, 450, 90, "subtitle"
    flow 0, -70, 950, 540, "story"
    flow -250, -70, 480, 540, "left"
    flow  250, -70, 480, 540, "right"
    flow 0, -70, 950, 540, "picture"
    flow -250, -70, 480, 540, "leftpic"
    flow  250, -70, 480, 540, "rightpic"
