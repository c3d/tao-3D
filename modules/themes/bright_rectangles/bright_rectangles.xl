// *****************************************************************************
// bright_rectangles.xl                                            Tao3D project
// *****************************************************************************
//
// File description:
//
//      Bright rectangles animated in the background
//
//
//
//
//
//
//
//
// *****************************************************************************
// This software is licensed under the GNU General Public License v3
// (C) 2013-2014, Baptiste Soulisse <baptiste.soulisse@taodyne.com>
// (C) 2012-2014,2019, Christophe de Dinechin <christophe@dinechin.org>
// (C) 2013-2014, Jérôme Forissier <jerome@taodyne.com>
// *****************************************************************************
// This file is part of Tao3D
//
// Tao3D is free software: you can r redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
//
// Tao3D is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
//
// You should have received a copy of the GNU General Public License
// along with Tao3D, in a file named COPYING.
// If not, see <https://www.gnu.org/licenses/>.
// *****************************************************************************

import Slides
import Charts

module_description
    id "3F8474A2-FB08-4463-AEF0-6D8E3A0AF92B"
    name "Bright Rectangles theme"
    description "A theme with bright rectangles animated in the background"
    import_name "BrightRectanglesTheme"
    author "Taodyne SAS"
    website "http://www.taodyne.com"
    version 1.004


module_description "fr",
    name "Rectangles Lumineux"
    description "Un thème avec des rectangles de couleur vives en fond d'écran"



// ============================================================================
//
//    Easily configurable parameters
//
// ============================================================================

theme_slide_width  "BrightRectangles"                  -> 1920
theme_slide_height "BrightRectangles"                  -> 1080


theme_background "BrightRectangles",Master:text        ->
// ----------------------------------------------------------------------------
//   Draw the rectangles in the background
// ----------------------------------------------------------------------------
    clear_color 0,0,0,1
    background -750,
        bright_rectangles_target := hours*60+minutes + 137 * page_number
        if abs(bright_rectangles_current - bright_rectangles_target) > 1000 then
            bright_rectangles_current := bright_rectangles_target
        bright_rectangles_current := 0.9 * bright_rectangles_current + 0.1 * bright_rectangles_target
        rectangle_background with 1..9
        if abs(bright_rectangles_current - bright_rectangles_target) > 0.2 then
            time


rectangle_background N:integer ->
// ----------------------------------------------------------------------------
//   Draw one rectangle
// ----------------------------------------------------------------------------
    color_hsv 42 * N + bright_rectangles_current, 1.0, 0.7, 0.9
    sw R:real,S:real -> slide_width * sin (R*bright_rectangles_current+S)
    sh R:real,S:real -> slide_height * cos (R*bright_rectangles_current+S)
    rectangle sw(121e-5,1.2*N), sh(133e-5,1.1*N), sw(232e-5,0.7*N), slide_height
    rectangle sw(142e-5,1.2*N), sh(127e-5,1.1*N), slide_width, sh(114e-5,0.7*N)

theme_color "BrightRectangles",Master:text,"picture"   -> color "white"
theme_color "BrightRectangles",Master:text,"story"     -> light 0, false; color "white"
theme_color "BrightRectangles",Master:text,"line",N:integer ->
    line_color_hsv (336.0 * N) / chart_datasets_count, 1.0, 0.7, 1.0
theme_color "BrightRectangles","pie",Style:text,N:integer ->
    color_hsv (336.0 * N) / (chart_count chart_first), 1.0, 0.7, 1.0

theme_color "BrightRectangles", "area", Style:text, N:integer -> theme_color "BrightRectangles", "bar", Style, N
theme_color "BrightRectangles", "line", Style:text, N:integer -> theme_color "BrightRectangles", "bar", Style, N
theme_color "BrightRectangles","bar",Style:text,N:integer ->
    color_hsv (336.0 * N) / chart_datasets_count, 1.0, 0.7, 1.0

theme_font "BrightRectangles",Master:text,"story"      -> font "Mountains of Christmas", "Times"
theme_boxstyle "BrightRectangles",Master:text,Style:text     -> color "black", 0.6

// No boxes for charts components (except title)
theme_box "BrightRectangles",Master:text,"chart_legend", X:real,Y:real,W:real,H:real -> false
theme_box "BrightRectangles",Master:text,"area", X:real,Y:real,W:real,H:real -> false
theme_box "BrightRectangles",Master:text,"bar", X:real,Y:real,W:real,H:real -> false
theme_box "BrightRectangles",Master:text,"line", X:real,Y:real,W:real,H:real -> false
theme_box "BrightRectangles",Master:text,"pie", X:real,Y:real,W:real,H:real -> false

theme_box "BrightRectangles",Master:text,Style:text, X:real,Y:real,W:real,H:real ->
// ----------------------------------------------------------------------------
//   Draw the background box for text and flows
// ----------------------------------------------------------------------------
    contents -10,
        theme_boxstyle theme, Master, Style
        path
            sx X:real,W:real,S:real -> X + W * (0.5 + 0.01 * sin(S * bright_rectangles_current + page_number))
            sy X:real,W:real,S:real -> X + W * (0.5 + 0.05 * cos(S * bright_rectangles_current + page_number))
            line_to sx(X,-W,222e-5), sy(Y,-H,235e-5)
            quad_to sx(X, W,432e-5), sy(Y,-H,229e-5), sx(X, W,442e-5), sy(Y,-H,249e-5)
            quad_to sx(X, W,217e-5), sy(Y, H,418e-5), sx(X, W,237e-5), sy(Y, H,218e-5)
            quad_to sx(X,-W,611e-5), sy(Y, H,910e-5),  sx(X,-W,211e-5), sy(Y, H,912e-5)
            quad_to sx(X,-W,322e-5), sy(Y,-H,225e-5), sx(X,-W,222e-5), sy(Y,-H,235e-5)


bright_rectangles_current -> 0.0
bright_rectangles_target -> 0.0
