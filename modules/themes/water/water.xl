// *****************************************************************************
// water.xl                                                        Tao3D project
// *****************************************************************************
//
// File description:
//
//   Blue Gold - A water-based theme
//
//
//
//
//
//
//
//
// *****************************************************************************
// This software is licensed under the GNU General Public License v3
// (C) 2013-2014, Baptiste Soulisse <baptiste.soulisse@taodyne.com>
// (C) 2013-2014,2019, Christophe de Dinechin <christophe@dinechin.org>
// (C) 2013-2014, Jérôme Forissier <jerome@taodyne.com>
// *****************************************************************************
// This file is part of Tao3D
//
// Tao3D is free software: you can r redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
//
// Tao3D is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
//
// You should have received a copy of the GNU General Public License
// along with Tao3D, in a file named COPYING.
// If not, see <https://www.gnu.org/licenses/>.
// *****************************************************************************

import Slides
import WaterSurface
import Charts

module_description
    id "d0dc1693-bb4e-4884-87b1-c2f29d437c58"
    name "Water theme"
    description "A theme with some nice water effects."
    import_name "WaterTheme"
    author "Taodyne SAS"
    website "http://www.taodyne.com"
    version 1.005


module_description "fr",
    name "Thème Water"
    description "Un thème comprenant divers effets d'eau."

add_search_path "Water:", module_dir


theme_slide_width  "Water" -> 1920
theme_slide_height "Water" -> 1080


// ============================================================================
//
//   Chart
//
// ============================================================================

// Chart font size
theme_size "Water", Master:text, "chart_data" -> 30
theme_size "Water", Master:text, "chart_label" -> 30
theme_size "Water", Master:text, "chart_tick" -> 30
theme_size "Water", Master:text, "chart_title" -> 50

// Chart colors
theme_color "Water", Master:text, "line", N:integer ->
    T := 0.8 / chart_datasets_count
    line_color_hsv 210 + (N - 1), 0.8 - (N - 1) * T, 1.0, 0.8
theme_color "Water", "pie", Style:text, N:integer ->
    T := 0.8 / ((chart_count chart_first) - 1)
    color_hsv 210 + (N - 1), 0.8 - (N - 1) * T, 1.0, 0.8
    line_color "black"
    line_width 0.3

theme_color "Water", "area", Style:text, N:integer -> theme_color "Water", "bar", Style, N
theme_color "Water", "line", Style:text, N:integer -> theme_color "Water", "bar", Style, N
theme_color "Water", "bar", Style:text, N:integer ->
    T := 0.8 / chart_datasets_count
    color_hsv 210 + (N - 1), 0.8 - T * (N - 1), 1.0, 0.8
    line_color "black"
    line_width 0.3


// ============================================================================
//
//   Global theme
//
// ============================================================================

theme_background "Water",Master:text ->
    clear_color 0, 0, 0, 1
    background -900,
        mouse_drop
        color "white"
        locally
            seconds
            add_random_drops "water_theme", 5
        // Bind underwater texture
        texture_unit 1
        texture "Water:background.jpg"
        colored_water_surface "water_theme", slide_width * 110%, slide_height * 110%

theme_size "Water",Master:text,"title"       -> 120
theme_size "Water",Master:text,"subtitle"    -> 80

theme_color "lightblue" -> "#3399FF"
theme_color "Water",Master:text,"title"     -> light 0, false; color theme_color "lightblue"
theme_color "Water",Master:text,"story"     -> color "black"

theme_font "Water",Master:text,Style:text     -> font "Yanone Kaffeesatz", "Yanone Kaffeesatz Regular"

theme_boxstyle "Water",Master:text,Style:text  -> color "white",0.2; line_color "lightblue", 0.50

theme_align "Water",Master:text,"story", N  -> margins 25+50*N, 0

theme_box "Water",Master:text,"picture", X:real,Y:real,W:real,H:real -> nil
theme_box "Water",Master:text,"leftpic", X:real,Y:real,W:real,H:real -> nil
theme_box "Water",Master:text,"rightpic", X:real,Y:real,W:real,H:real -> nil

theme_box "Water",Master:text,Style:text, X:real,Y:real,W:real,H:real ->
// ----------------------------------------------------------------------------
//   Draw the background box for text and flows
// ----------------------------------------------------------------------------
    contents -10,
        theme_boxstyle theme, Master, Style
        radial_gradient (W+10)/2, (H+10)/2, (W+H)/2, W+10, H+10,
            gradient_color 0.0, 0.9, 0.9, 1, 0.9
            gradient_color 0.5, 0.9, 0.9, 1, 0.95
            gradient_color 1.0, 0.9, 0.9, 1, 1.0
        rectangle X, Y, W + 10, H + 10


theme_bullet_picture "Water", Master:text, Level:integer ->
// ----------------------------------------------------------------------------
//    Draw theme bullets
// ----------------------------------------------------------------------------
    theme_bullet_circle S:real -> circle -50% * S, 30% * S, 20% * S
    color theme_color "lightblue"
    theme_bullet_circle theme_size ("Water", Master, "bullet", Level)

// ============================================================================
//
//   Water functions
//
// ============================================================================

mouse_drop ->
// ----------------------------------------------------------------------------
//    Add drops on the water surface with the mouse
// ----------------------------------------------------------------------------
    enable_selection_rectangle false
    locally
        add_drop "water_theme", mouse_x / (slide_width * 110%), mouse_y / (slide_height * 110%), 0.5, 0.08
