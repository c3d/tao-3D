// *****************************************************************************
// rectangles-theme.xl                                             Tao3D project
// *****************************************************************************
//
// File description:
//
//     Define a "Rectangles" theme
//
//
//
//
//
//
//
//
// *****************************************************************************
// This software is licensed under the GNU General Public License v3
// (C) 2012,2014,2019, Christophe de Dinechin <christophe@dinechin.org>
// *****************************************************************************
// This file is part of Tao3D
//
// Tao3D is free software: you can r redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
//
// Tao3D is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
//
// You should have received a copy of the GNU General Public License
// along with Tao3D, in a file named COPYING.
// If not, see <https://www.gnu.org/licenses/>.
// *****************************************************************************


// ============================================================================
//
//    Easily configurable parameters
//
// ============================================================================

rectangles_current -> 0.0
rectangles_target -> 0.0

theme_background "Rectangles",Master:text        ->
// ----------------------------------------------------------------------------
//   Draw the rectangles in the background
// ----------------------------------------------------------------------------
    clear_color 0,0,0,1
    background -750,
        rectangles_target := hours*60+minutes + 137 * page_number
        if abs(rectangles_current - rectangles_target) > 1000 then
            rectangles_current := rectangles_target
        rectangles_current := 0.9 * rectangles_current + 0.1 * rectangles_target
        rectangle_background with 1..9
        if abs(rectangles_current - rectangles_target) > 0.2 then
            time


rectangle_background N:integer ->
// ----------------------------------------------------------------------------
//   Draw one rectangle
// ----------------------------------------------------------------------------
    color_hsv 42 * N + rectangles_current, 1.0, 0.7, 0.9
    sw R:real,S:real -> slide_width * sin (R*rectangles_current+S)
    sh R:real,S:real -> slide_height * cos (R*rectangles_current+S)
    rectangle sw(121e-5,1.2*N), sh(133e-5,1.1*N), sw(232e-5,0.7*N), slide_height
    rectangle sw(142e-5,1.2*N), sh(127e-5,1.1*N), slide_width, sh(114e-5,0.7*N)


theme_color "Rectangles",Master:text,"picture"   -> color "white"
theme_color "Rectangles",Master:text,"story"     -> color "white"

theme_font "Rectangles",Master:text,"story"      -> font "Mountains of Christmas", "Times"
theme_boxstyle "Rectangles",Master:text,Style:text-> color "black", 0.6


theme_box "Rectangles",Master:text,Style:text, X:real,Y:real,W:real,H:real ->
// ----------------------------------------------------------------------------
//   Draw the background box for text and flows
// ----------------------------------------------------------------------------
    contents -10,
        theme_boxstyle theme, Master, Style
        path
            sx X:real,W:real,S:real -> X + W * (0.5 + 0.01 * sin(S * rectangles_current + page_number))
            sy X:real,W:real,S:real -> X + W * (0.5 + 0.05 * cos(S * rectangles_current + page_number))
            line_to sx(X,-W,222e-5), sy(Y,-H,235e-5)
            quad_to sx(X, W,432e-5), sy(Y,-H,229e-5), sx(X, W,442e-5), sy(Y,-H,249e-5)
            quad_to sx(X, W,217e-5), sy(Y, H,418e-5), sx(X, W,237e-5), sy(Y, H,218e-5)
            quad_to sx(X,-W,611e-5), sy(Y, H,910e-5),  sx(X,-W,211e-5), sy(Y, H,912e-5)
            quad_to sx(X,-W,322e-5), sy(Y,-H,225e-5), sx(X,-W,222e-5), sy(Y,-H,235e-5)


theme_slide_width  "Rectangles"                  -> 1920
theme_slide_height "Rectangles"                  -> 1080
