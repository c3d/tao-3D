// *****************************************************************************
// theme-quiz.xl                                                   Tao3D project
// *****************************************************************************
//
// File description:
//
//     General slide-based theme
//
//
//
//
//
//
//
//
// *****************************************************************************
// This software is licensed under the GNU General Public License v3
// (C) 2013, Catherine Burvelle <catherine@taodyne.com>
// (C) 2019, Christophe de Dinechin <christophe@dinechin.org>
// *****************************************************************************
// This file is part of Tao3D
//
// Tao3D is free software: you can r redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
//
// Tao3D is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
//
// You should have received a copy of the GNU General Public License
// along with Tao3D, in a file named COPYING.
// If not, see <https://www.gnu.org/licenses/>.
// *****************************************************************************

import Animate
import Slides
import Transitions

theme "theme_quiz"
transition_fade_through_color 0.071, 0.271, 0.478, 0.5

// ============================================================================
//
//   Theme quiz
//
// ============================================================================
common_background ->
    color "white"
    linear_gradient 0, 0, 0, slide_height, slide_width, slide_height,
        gradient_color 1, 0.07, 0.27, 0.48, 1
        gradient_color 0, 0.61, 0.74, 0.86, 1
    rectangle 0, 0, slide_width, slide_height
    color "white", 20%
    path
        move_to -slide_width / 2, slide_height / 3
        quad_to -slide_width / 6, slide_height / 6, -slide_width / 6, -3 / 2 * slide_height
        quad_to -slide_width / 6, slide_height / 10, -slide_width / 2, slide_height / 5
        move_to -slide_width / 2, slide_height / 7
        quad_to -slide_width / 6, slide_height / 10, -slide_width / 6, -3 / 2 * slide_height
        quad_to -slide_width / 6, slide_height / 15, -slide_width / 2, 0
        move_to -slide_width / 2, -slide_height / 30
        quad_to -slide_width / 6, slide_height / 66, -slide_width / 6, -3 / 2 * slide_height
        quad_to -slide_width / 6, slide_height / 100, -slide_width / 2, -slide_height / 10

theme_boxstyle "theme_quiz", "main","title"-> color "black", 0.0
theme_boxstyle "theme_quiz", Master:text,"title"-> color "white", 0.3
theme_boxstyle "theme_quiz", Master:text,Style:text-> color "black", 0.0


theme_box "theme_quiz",Master:text,"title", X:real,Y:real,W:real,H:real ->
// ----------------------------------------------------------------------------
//   Draw the background box for text and flows
// ----------------------------------------------------------------------------
    contents -10,
        theme_boxstyle "theme_quiz", Master, "title"
        rounded_rectangle X, Y, W + 10, H + 10, 35


theme_background "theme_quiz", "main" ->
// ----------------------------------------------------------------------------
//    Theme 1 title background
// ----------------------------------------------------------------------------
    background -6000,
        common_background
    contents -6000,
        color "white"
        image slide_width/4,slide_height/4,1,1,"logo_taoquiz.png"


theme_background "theme_quiz", Kind ->
// ----------------------------------------------------------------------------
//    Theme 1 basic background
// ----------------------------------------------------------------------------
    background -6000,
        common_background
    contents -6000,
        color "white"
        image slide_width/2 - 240, -slide_height/2 + 40,0.3,0.3,"logo_taoquiz.png"

