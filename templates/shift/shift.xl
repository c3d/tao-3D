// *****************************************************************************
// shift.xl                                                        Tao3D project
// *****************************************************************************
//
// File description:
//
//     Definitions for the Shift template.
//
//
//
//
//
//
//
//
// *****************************************************************************
// This software is licensed under the GNU General Public License v3
// (C) 2012,2014,2019, Christophe de Dinechin <christophe@dinechin.org>
// (C) 2012-2013, Jérôme Forissier <jerome@taodyne.com>
// *****************************************************************************
// This file is part of Tao3D
//
// Tao3D is free software: you can r redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
//
// Tao3D is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
//
// You should have received a copy of the GNU General Public License
// along with Tao3D, in a file named COPYING.
// If not, see <https://www.gnu.org/licenses/>.
// *****************************************************************************

import Animate

// ============================================================================
//
//   Slide layout
//
// ============================================================================


palm_green -> color 0, 0.37, 0.16


slide T:text, Body ->
// ----------------------------------------------------------------------------
//   Start new page, draw background and execute Body
// ----------------------------------------------------------------------------
    page T,
        compute_smooth_step
        draw_background
        locally Body


draw_background ->
// ----------------------------------------------------------------------------
//    Background : draw 2 palm trees
// ----------------------------------------------------------------------------
    scale3 window_scale
    locally
        palm_green
        translate 0, -600, -200
        rectangle 0, 0, 6000, 500

    color "white"
    locally
        color_hsv 200 + 10 * sin (0.1 * time) , 0.3, 0.75, 0.2
        locally
            translate -500, -250, -1400
            rotatez 5 * sin (0.1 * time)
            image 0, 0, 1, 1, "PalmTree1.png"
        locally
            translate 500, -300, -1600
            rotatez 4 * sin (0.17 * time)
            image 0, 0, 1, 1, "PalmTree2.png"


// ----------------------------------------------------------------------------
//   Auto resizing
// ----------------------------------------------------------------------------
scale3 S:real -> scale S, S, S
window_scale -> min (window_width / 1408, window_height / 768)


+X:text ->
// ----------------------------------------------------------------------------
//   Additional line of text
// ----------------------------------------------------------------------------
    paragraph_break
    text X


display_panel N:integer ->
// ----------------------------------------------------------------------------
//   Show the N-th panel
// ----------------------------------------------------------------------------
    if N > highest_step then highest_step := N
    if (step < 1) then skip 1
    if smooth_step >= (N-2)  and smooth_step <= (N+1) then
        locally
            translatex 350 + 700 * shift N
            panel page_label, N


shift N:integer ->
// ----------------------------------------------------------------------------
//    Shift the N-th element
// ----------------------------------------------------------------------------
    2 - fade_at(smooth_step, (N-2)) - fade_at(smooth_step, (N-1)) - fade_at(smooth_step, N) - fade_at(smooth_step, (N+1))


sep -> sep 20

sep H ->
// ----------------------------------------------------------------------------
//   Separator
// ----------------------------------------------------------------------------
    line_break
    color 0, 0, 0, 0
    rectangle 0, 0, 100, H
    color "black"
    line_break


title Body ->
// ----------------------------------------------------------------------------
//    Draw a title
// ----------------------------------------------------------------------------
    text_box 0, 0, 600, 700,
        color 0, 0.37, 0.16
        font "Verdana", 40, bold
        align 0.5
        vertical_align 0
        Body


subtitle Body ->
// ----------------------------------------------------------------------------
//   Display a subtitle
// ----------------------------------------------------------------------------
    color "black"
    font "Verdana", 36, bold
    paragraph_break
    Body
    sep 10


story_box Body ->
// ----------------------------------------------------------------------------
//   Story box
// ----------------------------------------------------------------------------
    text_box 0, 0, 580, 700,
        vertical_align 0.5
        align 0, 0
        render Body


story Subtitle:text, Bullet:text ->
// ----------------------------------------------------------------------------
//    Show a given story
// ----------------------------------------------------------------------------
     story_box
        if Subtitle <> "" then
            subtitle Subtitle
        bullet Bullet


bullet Body ->
// ----------------------------------------------------------------------------
//   Display a bullet point
// ----------------------------------------------------------------------------
    font "Verdana", 36, no_bold
    paragraph_break

    color 0, 0.37, 0.16
    rectangle 0, 12, 10, 10
    color "black"
    text "  "
    render Body


// ============================================================================
//
//   Animated pie chart
//
// ============================================================================


arc R:real -> 360 * R%
// ----------------------------------------------------------------------------
//   Percent to arc angle in degrees
// ----------------------------------------------------------------------------


arc_sector Start:real, Sweep:real, Hue:real, Legend:text ->
// ----------------------------------------------------------------------------
//    Draw the arc for a given investor
// ----------------------------------------------------------------------------
    arc_sector_helper fade_at(smooth_step, 2), Start, Sweep, Hue, Legend


arc_sector_helper Fade:real, Start:real, Sweep:real, Hue:real, Legend:text ->
// ----------------------------------------------------------------------------
//    Draw graphic elements
// ----------------------------------------------------------------------------
    locally
        color_hsv Hue, 0.4, 0.9
        ellipse_sector 0, 0, 300, 300, arc (Start*Fade), arc (Sweep*Fade)
        color "white", Fade
        locally
            translate 110 * cos(Fade*(Start+Sweep*0.5)% * 2 * pi), 110 * sin (Fade*(Start+Sweep*0.5)% * 2 * pi), 10
            color 0, 0.37, 0.16
            text_box 0, 0, 80, 40,
                font "Verdana", 25
                vertical_align_center
                align_center
                text text(integer(Sweep*Fade+0.5))
                text "%"
        locally
            translate 210 * cos(Fade*(Start+Sweep*0.5)% * 2 * pi), 210 * sin (Fade*(Start+Sweep*0.5)% * 2 * pi), 10
            color "black"
            text_box 0, 0, 200, 50,
                font "Verdana", 25
                vertical_align_center
                align_center
                text Legend
